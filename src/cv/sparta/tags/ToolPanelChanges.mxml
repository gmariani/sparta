<?xml version="1.0" encoding="utf-8"?>
<mx:VBox 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:mxi="cv.sparta.tags.*" 
	width="400" height="300" 
	label="Flash Only" 
	paddingBottom="10" paddingLeft="10" paddingRight="10" paddingTop="10">
	
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.events.ListEvent;
			
			[Bindable]
			private var acItems:ArrayCollection = new ArrayCollection([]);
			
			private var currentItemIndex:int = -1;
			
			public function set languages(data:ArrayCollection):void {
				tagItemInsert.acLanguages = data;
			}
			
			public function get languages():ArrayCollection {
				return tagItemInsert.acLanguages;
			}
		
			public function reset():void {
				acItems = new ArrayCollection([]);
				cancel();
			}
			
			public function setXML(value:XML):void {
				reset();
				
				acItems = new ArrayCollection([]);
				for each(var f:XML in value["configuration-changes"]["toolpanel-changes"]["toolpanel-item-insert"]) {
					acItems.addItem(tagItemInsert.xmlToObject(f));
				}
			}
			
			public function getXML():String {
				var str:String = "";
				if(acItems.length > 0) {
					str += '<toolpanel-changes>';
					for(var i:String in acItems) {
						str += tagItemInsert.objectToString(acItems[i]);
					}
					str += '</toolpanel-changes>';
				}
				return str;
			}
			
			private function add():void {
				show(tagItemInsert.getEmptyData(), -1);
			}
			
			private function save():void {
				var obj:Object = tagItemInsert.data;
				
				if(currentItemIndex < 0) {
					acItems.addItem(obj);
					listItems.selectedItem = obj;
					currentItemIndex = listItems.selectedIndex;
				} else {
					acItems.setItemAt(obj, currentItemIndex);
					listItems.selectedIndex = currentItemIndex;
				}
				
				vsToolPanel.selectedIndex = 0;
			}
			
			private function resetItem():void {
				
				listItems.selectedItem = null;
			}
			
			private function remove():void {
				acItems.removeItemAt(currentItemIndex);
				currentItemIndex = -1;
				listItems.selectedIndex = currentItemIndex;
				
				cancel();
			}
			
			private function cancel():void {
				tagItemInsert.reset();
				
				vsToolPanel.selectedIndex = 0;
			}
			
			private function show(obj:Object, index:int):void {
				currentItemIndex = index;
				
				tagItemInsert.data = obj;
				vsToolPanel.selectedIndex = 1;
			}
			
			private function listLabel(obj:Object):String {
				return tagItemInsert.objectToString(obj);
			}
			
			private function initListEditor(e:ListEvent):void {
				listItems.itemEditorInstance.addEventListener("SAVE", listEditorHandler);
				listItems.itemEditorInstance.addEventListener("REMOVE", listEditorHandler);
			}
			
			private function destroyListEditor(e:ListEvent):void {
				listItems.itemEditorInstance.removeEventListener("SAVE", listEditorHandler);
				listItems.itemEditorInstance.removeEventListener("REMOVE", listEditorHandler);
			}
			
			private function listEditorHandler(e:Event):void {
				if(e.type == "SAVE") {
					show(listItems.selectedItem, listItems.selectedIndex);
				} else {
					remove();
				}
			}
		]]>
	</mx:Script>
	
	<mx:FormHeading label="Tool Panel Changes" width="100%">
		<mx:toolTip>Marks the beginning of Flash tool panel changes.</mx:toolTip>
	</mx:FormHeading>
	
	<mx:ViewStack id="vsToolPanel" width="100%" height="100%">
		<mx:VBox width="100%" height="100%">
			<mx:HBox width="100%" horizontalAlign="right" >
				<mx:Button label="Add Tool Panel Item" click="add()" toolTip="Inserts the tool with the specified name into the Flash tool panel." >
					<mx:icon>@Embed(source='../../../assets/icons/addIcon.png')</mx:icon>
				</mx:Button>
			</mx:HBox>
			
			<mx:List 
				editable="true" 
				itemEditor="cv.sparta.editor.ChildEditor"
				editorYOffset="23"
				editorDataField="data" editorHeightOffset="7"
				itemEditEnd="destroyListEditor(event)" 
				itemFocusIn="initListEditor(event)"
				id="listItems" width="100%" height="100%" labelFunction="listLabel" dataProvider="{acItems}" />
		</mx:VBox>
		
		<mxi:ToolPanelItemInsert id="tagItemInsert" width="100%" height="100%" save="save()" cancel="cancel()" paddingBottom="0" paddingLeft="0" paddingRight="0" paddingTop="0" />
	</mx:ViewStack>
</mx:VBox>