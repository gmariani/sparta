<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" width="500" title="Dreamweaver Toolbar Include">
	
	<mx:Script>
		<![CDATA[
			import mx.managers.PopUpManager;
			import mx.events.ValidationResultEvent;
			import flash.filesystem.File;
			import mx.validators.Validator;
			import mx.controls.Alert;
			
			override public function set data(obj:Object):void {
				reset();
				
				txtFile.text = obj.data.file;
			}
			
			override public function get data():Object {
				var obj:Object = new Object();
				obj.data = new Object();
				obj.data.file = txtFile.text;
				obj.data.tagType = "Include";
				return obj;
			}
			
			public static function getEmptyData():Object {
				var obj:Object = new Object();
				obj.data = new Object();
				obj.data.file = "";
				obj.data.tagType = "Include";
				return obj;
			}
			
			public static function xmlToObject(f:XML):Object {
				var obj:Object = new Object();
				obj.data = new Object();
				
				obj.data.file = "";
				if("@file" in f) obj.data.file = f.@file.toString();
				
				obj.data.tagType = "Include";
				
				return obj;
			}
			
			public static function objectToString(obj:Object):String {
				var str:String = '<include';
				str += ' file="' + obj.data.file + '"';
				str += ' />';
				return str;
			}
			
			public function reset():void {
				txtFile.text = "";
			}
			
			private function validateForm():void {
            	var validatorArr:Array = [valFile];
            	var validatorErrorArray:Array = Validator.validateAll(validatorArr);
            	if (validatorErrorArray.length == 0) {
                    dispatchEvent(new Event('SAVE'));
                    resetForm();
                } else {
                    var err:ValidationResultEvent;
                    var errorMessageArray:Array = [];
                    for each (err in validatorErrorArray) {
                    	var errField:String;
                    	if(err.currentTarget.source.parent is HBox) {
                    		errField = FormItem(err.currentTarget.source.parent.parent).label;
                    	} else {
                    		errField = FormItem(err.currentTarget.source.parent).label;
                    	}
                        errorMessageArray.push(errField + ": " + err.message);
                    }
                    Alert.show(errorMessageArray.join("\n\n"), "Invalid Include...", Alert.OK);
                }
            }
            
            private function resetForm():void {
            	PopUpManager.removePopUp(this);
            }
            
            private function onClickBrowse():void {
				var f:flash.filesystem.File = flash.filesystem.File.desktopDirectory;
				f.addEventListener(Event.SELECT, onFileSelection);
				f.browseForOpen("Select File");
			}
			
			private function onFileSelection(event:Event):void {
				txtFile.text = flash.filesystem.File(event.target).nativePath;
			}
		]]>
	</mx:Script>
	
	<mx:StringValidator id="valFile" required="true" source="{txtFile}" property="text" />

	<mx:Form width="100%" height="100%">
		<mx:FormItem label="File" width="100%" height="100%" required="true">
			<mx:toolTip>The file path, relative to the Toolbars folder, of the toolbar XML file to include.</mx:toolTip>
			<mx:HBox width="100%">
				<mx:TextInput id="txtFile" width="100%" />
				<mx:Button label="Browse" click="onClickBrowse()"/>
			</mx:HBox>
		</mx:FormItem>
		
		<mx:FormItem>
			<mx:HBox>
				<mx:Button label="Ok" click="validateForm()"/>
				<mx:Button label="Cancel" click="resetForm()"/>
			</mx:HBox>
		</mx:FormItem>
	</mx:Form>
</mx:TitleWindow>